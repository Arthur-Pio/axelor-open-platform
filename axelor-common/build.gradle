description = 'Axelor\'s common APIs'

apply plugin: 'java'

dependencies {

  testImplementation libs.junit_jupiter

  implementation libs.guava
  implementation libs.asm
  implementation libs.commons_csv
  implementation libs.apache_tika_core
}

configurations {
  runtime.exclude module: 'jsr305'

  // Remove Junit4, only keep Junit5
  testImplementation.exclude group: 'junit', module: 'junit'
  testImplementation.exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}

void writeManifest(File dir, boolean force) {
  if (force || dir.exists()) {
    jar.manifest.writeTo(file("${dir}/META-INF/MANIFEST.MF"))
  }
}

afterEvaluate {
  writeManifest(file("${project.buildDir}/resources/main"), true)
  writeManifest(file("${projectDir}/bin/main"), false) // to eclipse output dir
  writeManifest(file("${projectDir}/out/production/resources"), false) // to intelij output dir
}

jar {
  manifest {
    attributes(
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
      'Implementation-Vendor': 'Axelor',
      'Implementation-Vendor-Id': 'com.axelor',
      'Implementation-Vendor-Url': 'http://axelor.com',
      'Specification-Title': project.name,
      'Specification-Version': project.version,
      'Specification-Vendor': 'Axelor')
  }
}

def libs = ext.libs = [:]
def versions = [:]

// versions
versions.guice = "5.1.0"
versions.guava = "31.1-jre"
versions.hibernate = "5.6.5.Final"
versions.hibernate_validator = "6.2.2.Final"
versions.hibernate_search = "5.11.9.Final"

versions.groovy = "2.4.10"
versions.resteasy = "4.7.5.Final"
versions.jackson = "2.13.2"
versions.shiro = "1.8.0"
versions.buji_pac4j = "4.1.1"
versions.pac4j = "3.8.3"
versions.ldaptive = "1.2.4"

versions.junit4 = "4.13.2"
versions.junit5 = "5.8.2"

versions.slf4j = "1.7.35"

versions.tomcat = "8.5.54"
versions.tomcat_embed = "${versions.tomcat}"

// unit testing
libs.junit = "junit:junit:${versions.junit4}"
libs.junit_jupiter = "org.junit.jupiter:junit-jupiter:${versions.junit5}"
libs.junit_vintage = "org.junit.vintage:junit-vintage-engine:${versions.junit5}" // junit4 compatibility
libs.junit_launcher = "org.junit.platform:junit-platform-launcher:1.8.2" // for IDE

// logging
libs.slf4j_base = [
  "org.slf4j:slf4j-api:${versions.slf4j}",
  "org.slf4j:jul-to-slf4j:${versions.slf4j}",
  "org.slf4j:jcl-over-slf4j:${versions.slf4j}",
  "org.slf4j:log4j-over-slf4j:${versions.slf4j}"
]

libs.slf4j_logback = libs.slf4j_base + [
  "ch.qos.logback:logback-core:1.2.11",
  "ch.qos.logback:logback-classic:1.2.11",
  "org.fusesource.jansi:jansi:2.4.0"
]

// Guava
libs.guava = [
  "com.google.guava:guava:${versions.guava}",
  "com.google.code.findbugs:jsr305:3.0.2"
]

// yaml
libs.snakeyaml = "org.yaml:snakeyaml:1.30"

// Guice
libs.guice = "com.google.inject:guice:${versions.guice}"
libs.guice_servlet = "com.google.inject.extensions:guice-servlet:${versions.guice}"
libs.guice_persist = "com.google.inject.extensions:guice-persist:${versions.guice}"

// ASM
libs.asm = [
  "org.ow2.asm:asm:9.2",
  "org.ow2.asm:asm-commons:9.2",
  "org.ow2.asm:asm-util:9.2",
  "org.ow2.asm:asm-tree:9.2",
  "org.ow2.asm:asm-analysis:9.2"
]

// Byte Buddy
libs.byte_buddy = "net.bytebuddy:byte-buddy:1.11.22"

// Tomcat EL (repackaged to avoid version conflicts)
libs.axelor_el = "com.axelor:axelor-el:${versions.tomcat}"

// javax libs
libs.javax_jaxb = "javax.xml.bind:jaxb-api:2.3.1"
libs.javax_servlet = "javax.servlet:javax.servlet-api:4.0.1"
libs.javax_servlet_jsp = "javax.servlet.jsp:javax.servlet.jsp-api:2.3.3"
libs.javax_validation = "javax.validation:validation-api:2.0.1.Final"
libs.javax_el = ["org.apache.tomcat:tomcat-jasper:${versions.tomcat}", "org.apache.tomcat:tomcat-el-api:${versions.tomcat}"]
libs.javax_mail = "com.sun.mail:javax.mail:1.6.2"
libs.greenmail = "com.icegreen:greenmail-junit5:1.6.7"
libs.javax_el = "org.glassfish:jakarta.el:3.0.4"

// jdbc
libs.jdbc_hsqldb = "org.hsqldb:hsqldb:2.3.4"
libs.jdbc_postgres = "org.postgresql:postgresql:42.0.0"
libs.jdbc_mysql = "mysql:mysql-connector-java:5.1.41"

// Hibernate
libs.hibernate = [
  "org.hibernate:hibernate-core:${versions.hibernate}",
  "org.hibernate:hibernate-hikaricp:${versions.hibernate}",
  "org.hibernate:hibernate-validator:${versions.hibernate_validator}",
  "org.hibernate:hibernate-ehcache:${versions.hibernate}",
  "org.hibernate:hibernate-jcache:${versions.hibernate}"
]

// Hibernate search
libs.hibernate_search = "org.hibernate:hibernate-search-orm:${versions.hibernate_search}"

// apache tika
libs.apache_tika_core = "org.apache.tika:tika-core:1.4"

// minimal set of tika-parsers
libs.apache_tika_parsers = dependencies.create("org.apache.tika:tika-parsers:1.4") {
  exclude group: 'org.gagravarr', module: 'vorbis-java-tika'
  exclude group: 'edu.ucar', module: 'netcdf'
  exclude group: 'org.apache.james', module: 'apache-mime4j-core'
  exclude group: 'org.apache.james', module: 'apache-mime4j-dom'
  exclude group: 'org.bouncycastle', module: 'bcmail-jdk15'
  exclude group: 'org.bouncycastle', module: 'bcprov-jdk15'
  exclude group: 'org.ow2.asm', module: 'asm-debug-all'
  exclude group: 'com.googlecode.mp4parser', module: 'isoparser'
  exclude group: 'xerces', module: 'xercesImpl'
  exclude group: 'rome', module: 'rome'
  exclude group: 'org.gagravarr', module: 'vorbis-java-core'
  exclude group: 'org.apache.geronimo.specs', module: 'geronimo-stax-api_1.0_spec'
}

// flyway
libs.flywaydb = "org.flywaydb:flyway-core:8.5.4"

// eclipselink
libs.eclipselink_moxy = "org.eclipse.persistence:org.eclipse.persistence.moxy:2.7.10"

// groovy
libs.groovy = dependencies.create("org.codehaus.groovy:groovy-all:${versions.groovy}") {
    exclude group: "org.junit.platform"
}
libs.groovy_wslite = dependencies.create("com.github.groovy-wslite:groovy-wslite:1.1.2") { exclude group: "org.codehaus.groovy" }

def resteasy_module = { name ->
    dependencies.create("org.jboss.resteasy:${name}:${versions.resteasy}") {
        exclude module: "smallrye-config"
        exclude module: "resteasy-jaxb-provider"
        exclude module: "mail", group: "javax.mail"
    }
}

// jax-rs
libs.resteasy = [
    resteasy_module("resteasy-core"),
    resteasy_module("resteasy-jackson2-provider"),
    "io.smallrye.config:smallrye-config:1.6.1"
]

libs.resteasy_multipart = resteasy_module("resteasy-multipart-provider")
libs.resteasy_guice = resteasy_module("resteasy-guice")
libs.resteasy_client = resteasy_module("resteasy-client");
libs.resteasy_server = [
  "org.jboss.resteasy:resteasy-undertow:${versions.resteasy}",
  "io.undertow:undertow-core:2.2.16.Final",
  "io.undertow:undertow-servlet:2.2.16.Final"
]

// jackson
libs.jackson_libs = [
  "com.fasterxml.jackson.core:jackson-core:${versions.jackson}",
  "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}",
  "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}",
  "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${versions.jackson}",
  "com.fasterxml.jackson.datatype:jackson-datatype-guava:${versions.jackson}",
  "com.fasterxml.jackson.module:jackson-module-parameter-names:${versions.jackson}",
  "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${versions.jackson}",
  "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${versions.jackson}"
]

// Apache Shiro
libs.apache_shiro = [
  "org.apache.shiro:shiro-core:${versions.shiro}",
  "org.apache.shiro:shiro-web:${versions.shiro}",
  dependencies.create("org.apache.shiro:shiro-guice:${versions.shiro}") {
      // Shiro 1.8 still depends on guice-multibindings 4.2.2 (included as part of Guice core)
      exclude group: 'com.google.inject.extensions', module: 'guice-multibindings'
  }
]

libs.pac4j = [
  "io.buji:buji-pac4j:${versions.buji_pac4j}",
  dependencies.create("org.pac4j:pac4j-oidc:${versions.pac4j}") {
    exclude group: 'org.mockito', module: 'mockito-core'
  },
  "org.pac4j:pac4j-oauth:${versions.pac4j}",
  dependencies.create("org.pac4j:pac4j-saml:${versions.pac4j}") {
    exclude group: 'xml-apis', module: 'xml-apis'
    exclude group: 'org.hibernate', module: 'hibernate-entitymanager'
    exclude group: 'org.hibernate.javax.persistence', module: 'hibernate-jpa-2.1-api'
  },
  "org.pac4j:pac4j-cas:${versions.pac4j}",
  "org.pac4j:pac4j-http:${versions.pac4j}",
  "org.pac4j:pac4j-ldap:${versions.pac4j}",
  "org.ldaptive:ldaptive:${versions.ldaptive}"
]

libs.pac4j_core = [
  "org.pac4j:pac4j-core:${versions.pac4j}"
]

// Apache DS (for testing only)
libs.apache_ds = [
  "org.apache.directory.jdbm:apacheds-jdbm1:2.0.0-M3",
  dependencies.create("org.apache.directory.server:apacheds-test-framework:2.0.0-M15") {
    exclude group: "org.apache.directory.jdbm"
    exclude group: "bouncycastle"
  }
]

// commons
libs.commons_cli = "commons-cli:commons-cli:1.5.0"
libs.commons_jxpath = "commons-jxpath:commons-jxpath:1.3"
libs.commons_csv = [
  "org.apache.commons:commons-csv:1.9.0",
  "commons-io:commons-io:2.11.0"
]

// BIRT Runtime
libs.birt_runtime = [
  dependencies.create("org.eclipse.birt.runtime:org.eclipse.birt.runtime:4.4.2") {
    exclude module: "derby"
    exclude module: "org.eclipse.datatools.connectivity.apache.derby"
    exclude module: "org.eclipse.datatools.connectivity.apache.derby.dbdefinition"
    exclude module: "org.apache.batik.pdf" // causing issue with commons-io
    exclude module: "commons-cli"
    exclude module: "xml-apis"
    exclude module: "javax.xml.stream"
    exclude module: "org.w3c.dom.smil"
    exclude module: "org.apache.xerces"
    exclude module: "xmlbeans"
    exclude module: "flute"
  },
  // overrides
  dependencies.create("xerces:xercesImpl:2.9.0") {
    exclude module: "xml-apis"
  },
  "milyn:flute:1.3",
  "org.apache.xmlbeans:xmlbeans:3.1.0"
]

// Quartz Scheduler
libs.quartz = dependencies.create("org.quartz-scheduler:quartz:2.3.2") {
  exclude module: "c3p0"
}

// xstream
libs.xstream = [
  "com.thoughtworks.xstream:xstream:1.4.19",
  "com.fasterxml.woodstox:woodstox-core:6.2.8"
]

// jsoup for html parsing & sanitize
libs.jsoup = "org.jsoup:jsoup:1.14.3"

// StringTemplate
libs.ST4 = "org.antlr:ST4:4.3.1"

// gradle tooling
libs.gradle_tooling = "org.gradle:gradle-tooling-api:${gradle.gradleVersion}"

// gradle plugins
libs.plugin_license = "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1"

// embeded tomcat
libs.tomcat_embed = [
  "org.apache.tomcat.embed:tomcat-embed-core:${versions.tomcat_embed}",
  "org.apache.tomcat.embed:tomcat-embed-jasper:${versions.tomcat_embed}",
  libs.javax_el
]

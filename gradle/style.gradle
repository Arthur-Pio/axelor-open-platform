buildscript {
  repositories repos
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:5.15.0"
  }
}

apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

spotless {
  format 'misc', {
    target fileTree(projectDir) {
      include '**/.gitignore'
      include '**/*.gradle'
      include '**/*.adoc'
      include '**/*.sh'
      include '**/*.html'
      include '**/*.js'
      include '**/*.css'
      include '**/*.xml'
      include '**/*.xsd'
      exclude '**/webapp/lib/*'
      exclude '**/webapp/dist/*'
      exclude '**/webapp/node_modules/*'
      exclude '**/build/*'
    }
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
  format 'markdown', {
    target '**/*.md'
    indentWithSpaces(2)
    endWithNewline()
  }
}

spotlessMisc.dependsOn(spotlessMarkdown)

subprojects { p ->
  apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin
  spotless {
    java {
      googleJavaFormat()
    }
  }
  task formatCode(dependsOn: ['licenseFormat', 'spotlessApply'])

  // fix up-to-date check issue
  p.afterEvaluate {
    try {
      p.spotlessJava.dependsOn(p.generateCode)
    } catch (Exception e) {
    }
  }
}

task formatCode(dependsOn: 'spotlessApply')

allprojects {
  build.dependsOn 'spotlessApply'
}

description = 'Gradle plugin for integrating Axelor'

apply plugin: 'java-gradle-plugin'

configurations {
  tomcat
  tomcat.transitive false
  provided
  provided.transitive false
  database
  database.transitive false

  // Remove Junit4, only keep Junit5
  testImplementation.exclude group: 'junit', module: 'junit'
  testImplementation.exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}

dependencies {
  implementation project(':axelor-common')
  implementation project(':axelor-tools')

  implementation libs.guava
  implementation libs.commons_csv
  implementation libs.plugin_license

  tomcat project(":axelor-tomcat")

  provided libs.javax_servlet
  provided libs.javax_servlet_jsp
}

gradlePlugin {
  plugins {
    axelorPlugin {
      id = "com.axelor.app"
      implementationClass = "com.axelor.gradle.AxelorPlugin"
    }
  }
}

def generateLibsList(config) {
  def outDir = new File(project.buildDir, "resources/main/com/axelor/gradle")
  def outFile = new File(outDir, "${config}-libs.txt");
  outDir.mkdirs()
  outFile.text = ""
  configurations.getByName(config).dependencies.each {
    outFile << "${it.group}:${it.name}:${it.version}\n"
  }
}

classes.doLast {
  generateLibsList "tomcat"
  generateLibsList "database"
  generateLibsList "provided"
}

// prevent double publish
tasks.withType(PublishToMavenRepository).all {
  enabled = name != 'publishMavenJavaPublicationToMavenRepository'
}
